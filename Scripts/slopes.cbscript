dir "C:\Users\Seth\AppData\Roaming\.minecraft 1.14\saves\SethBling's World 1.14"
desc "Create 45 degree slopes"

import common
import miniblock

$offsets = [
	(0.1872, 0.121, -0.03),
	(-0.1872, 0.12, -0.03),
	(-0.1872, 0.561, -0.47),
	(0.1872, 0.56, -0.47),
	(-0.1872, -0.321, 0.41),
	(0.1872, -0.32, 0.41),
	(-0.1872, -0.19, -0.6),
	(0.1872, -0.19, -0.6)
]

define @Slope : @Marker[tag=Slope]
	array blocks[$len($offsets)]
end

define @Physics: @Entity[type=armor_stand, tag=Physics]
	create {Tags:["Physics"], Marker:1b, Invisible:1b, Invulnerable:1b, NoGravity:1b, Passengers:[{id:"shulker", NoGravity:1b, Invulnerable:1b, Invisible:1b, NoAI:1b, Silent:1b, Tags:["SolidBlock"]}]}
end

@SolidBlock = @Entity[type=shulker, tag=SolidBlock]

reset
	/scoreboard objectives add slide minecraft.custom:minecraft.sneak_time
	/scoreboard objectives add create_slope minecraft.used:minecraft.carrot_on_a_stick
end

function create_slope()
	align xyz at ~0.5 ~ ~0.5
		unless @Position do create @Position
		/tp @Position ~ ~ ~ ~ ~
		as create @Marker
			/tag @s add Slope
			try = @Position.ry % 360000
			ry = -1
			if try >= 45000 and try < 135000 and block ~1 ~ ~ air unless block ~-1 ~ ~ air
				ry = 270000
			else if try >= 135000 and try < 225000 and block ~ ~ ~1 air unless block ~ ~ ~-1 air
				ry = 0
			else if try >= 225000 and try < 315000 and block ~-1 ~ ~ air unless block ~1 ~ ~ air
				ry = 90000
			else if try < 45000 and block ~ ~ ~-1 air unless block ~ ~ ~1 air
				ry = 180000
			else if try >= 315000 and block ~ ~ ~-1 air unless block ~ ~ ~1 air
				ry = 180000
			else if block ~1 ~ ~ air unless block ~-1 ~ ~ air
				ry = 270000
			else if block ~-1 ~ ~ air unless block ~1 ~ ~ air
				ry = 90000
			else if block ~ ~ ~1 air unless block ~ ~ ~-1 air
				ry = 0
			else if block ~ ~ ~-1 air unless block ~ ~ ~1 air
				ry = 180000
			end
			
			unless ry == -1
				@s.ry = ry
				@Marker.current = False
				@s.current = True
				
				for $i in $range($len($offsets))
					rotated @s at ^<$offsets[$i]>
						@s.blocks[$i] = create @MiniBlock ^ ^-1.6 ^-0.22
						as @s.blocks[$i] (@MiniBlock)
							@s.slope = True
							at @Marker[current,limit=1]
								if block ~ ~ ~ grass_block
									/replaceitem entity @s armor.head grass_block
								else if block ~ ~ ~ snow_block
									/replaceitem entity @s armor.head snow_block
								else
									/loot replace entity @s armor.head mine ~ ~ ~ minecraft:diamond_pickaxe
								end
							end
							@s.rot_x = 45000
							@s.rot_y = ry
							@s.update()
						end
					end
				end
				if @s.ry == 0
					/setblock ~ ~ ~ tripwire[east=true,west=true]
				end
				if @s.ry == 90000
					/setblock ~ ~ ~ tripwire[north=true,south=true]
				end
				if @s.ry == 180000
					/setblock ~ ~ ~ tripwire[east=true,west=true]
				end
				if @s.ry == 270000
					/setblock ~ ~ ~ tripwire[north=true,south=true]
				end
				
				as create @Physics ~ ~-1 ~
					/tp @s ~ ~ ~
					@s.ry = ry
					move @s ^ ^-0.5 ^-0.5
				end
			end
		end
	end
end

function kill_slopes()
	at @Slope if block ~ ~ ~ tripwire
		/setblock ~ ~ ~ air
	end
	/kill @Slope
	/kill @MiniBlock[slope]
	/kill @SolidBlock
	/kill @Physics
end

clock main
	as @SolidBlock
		/effect give @s invisibility 100 1 true
	end
	as @Slope at @s
		unless block ~ ~ ~ tripwire
			if success
			/kill @e[type=item,nbt={Item:{id:"minecraft:string"}},distance=..1]
				as @s.blocks[0]
					/summon item ~ ~0.5 ~ {Item:{id:"minecraft:stone", Count:1b, PickupDelay: 20},Tags:["spawned_item"]}
					@Entity[type=item,tag=spawned_item,limit=1].{Item} += @s.{ArmorItems[3]}
					/tag @Entity[type=item,tag=spawned_item,limit=1] remove spawned_item
				end
			end
			for $i in $range($len($offsets))
				as @s.blocks[$i]
					/tp @s ~ -100 ~
					/kill @s
				end
			end
			as @SolidBlock[distance=..1]
				/tp @s ~ -100 ~
				/kill @s
			end
			as @Physics[distance=..1]
				/tp @s ~ -100 ~
				/kill @s
			end
			/kill @s
		end
	end
	
	as @Player
		if @s.slide at @s if @Slope[distance=..1.3,limit=1]
			@s.slide_count++
		else
			@s.slide_count = 0
		end
		
		at @s[slide_count] rotated @Slope[distance=..1.3,limit=1]
			switch @s.slide_count
				case $i in $range(50)
					$dist = $float($i) * 0.01
					$vdist = -$dist
					/tp @s ^ ^$vdist ^$dist
				end
				case 50 to 100000	
					$dist = $float(50) * 0.01
					$vdist = -$dist
					/tp @s ^ ^$vdist ^$dist
				end
			end
			for i = 1 to 20
				at @s unless block ~ ~ ~ air
					/tp @s ~ ~0.1 ~
				end
			end
		end
		
		if @s[nbt={"SelectedItem":{tag:{display:{Name:"{\"text\":\"Sloper\"}"}}}}] and @s.create_slope
			steps = 0
			at @s ~ ~1.6 ~
				while block ^ ^ ^ air and steps < 100 at ^ ^ ^0.1
					steps++
					unless block ^ ^ ^ air unless block ^ ^ ^ tripwire
						create_slope()
						steps = 1000
					end
				end
			end
		end
	end
	
	@Player.create_slope = False
	
	as @Player[slide] at @s rotated @Slope[distance=..1,limit=1]
		/tp @s ^ ^ ^0.1
	end
	@Player.slide = 0
	
	as @MiniBlock[slope]
		@s.fire = 0
	end
end