dir "C:\Users\Seth\AppData\Roaming\.minecraft 1.14\saves\SethBling's World 1.14"
desc "Transform structures into other structures"


import common
import random

define @Spit : @Entity[type=llama_spit, tag=Spit]
	create {
		Tags: ["Spit"],
	}
end

define @Tornado : @Entity[type=area_effect_cloud, tag=Tornado]
	create {
		Tags:["Tornado"],
		Duration:-1,
		Age:-0x80000000,
		WaitTime:-0x80000000
	}
end

reset
	tell @a '[\[Spawn Tornado\]](call spawn_tornado)'
end

function spawn_tornado()
	create @Tornado
end

clock main
	unless @Position do create @Position

	as @Tornado at @s
		for i = 1 to 30
			height = randint(0, 30000)
			@Position.x = @s.x
			@Position.y = @s.y + height
			@Position.z = @s.z
			angle = randint(0, 360) * scale
			@Position.ry = angle
			@Position.rx = 0
			<pos1> = @Position.<pos>
			move @Position ^ ^ ^1
			<dx, dy, dz> = @Position.<pos> - <pos1>
			move @Position ^ ^ ^-1
			@Position.<pos> += <dx, dy, dz> * height / 2000
			at @Position
				as create @Spit
					@s.<vel> = <-dz, 200, dx> / 4
				end
			end		
		end
	
		@s.<pos> += @s.<v>
		@s.<v> += <randint(-10, 11), 0, randint(-10, 11)>
	end

	as @Spit
		@s.air = 1
		@s.air = 0
		
		@s.age++
		if @s.age >= 20
			/kill @s
		end
	end
end