dir "C:\Users\Seth\AppData\Roaming\.minecraft 1.14\saves\SethBling's World 1.14"
desc "A redstone component that rotates blocks."

import common

define @RotatorMarker: @Entity[type=area_effect_cloud,tag=RotatorMarker]
	create {Tags:["RotatorMarker"], Duration:-1,Age:-2147483648,WaitTime:-2147483648}
end

advancement placed_rotator {
	criteria: {
		place_rotator: {
			trigger: "minecraft:placed_block",
			conditions: {
				block: "minecraft:redstone_lamp"
			}
		}
	},
	rewards: {
		"function": "$namespace:placed_rotator"
	}
}

define @Player: @Player
	function placed_rotator()
		/advancement revoke @s only rotator:placed_rotator

		at @s eyes ^ ^ ^
			steps = 0
			while steps < 60 at feet ^ ^ ^0.1
				if block ~ ~ ~ redstone_lamp align xyz at ~0.5 ~0.5 ~0.5 unless @RotatorMarker[distance=..0.1]
					if block ~ ~ ~ redstone_lamp
						steps = 1000000
						as create @RotatorMarker

						end
					end
				end
				steps++
			end
		end
	end
end

define [Moving]
	extending: extending byte 1
	face: facing int 1
	source: source byte 1
	progress: progress float 2
end

$Deltas = [
	(0, -1, 0),
	(0, 1, 0),
	(0, 0, -1),
	(0, 0, 1),
	(-1, 0, 0),
	(1, 0, 0),
]

$OppositeDir = [1, 0, 3, 2, 5, 4]

reset
end

clock main
	as @RotatorMarker at @s
		# Check for piston pushes, and move if so

		unless block ~ ~ ~ redstone_lamp
			moved = False
			for $direction in $range(6)
				unless moved
					$dx, $dy, $dz = $Deltas[$direction]
					moved_dir = False
					/execute if data block ~$dx ~$dy ~$dz {id:"minecraft:piston", source:0b, progress:0.0f, extending:1b, facing: $direction} run scoreboard players set Global moved_dir 1

					$op = $OppositeDir[$direction]
					/execute if data block ~$dx ~$dy ~$dz {id:"minecraft:piston", source:0b, progress:0.0f, extending:0b, facing: $op} run scoreboard players set Global moved_dir 1

					if moved_dir
						moved = True
						@s.moving_timer = 3
						move @s ~$dx ~$dy ~$dz
					end
				end
			end

			unless @s.moving_timer > 0
				/kill @s
			else
				moving_timer--
			end
		end

		if @s.powered
			if block ~ ~ ~ redstone_lamp[lit=false]
				@s.powered = False
			end
		else
			if block ~ ~ ~ redstone_lamp[lit=true]
				@s.powered = True
				at ~ ~1 ~
					id = get_block()
					rotate_clockwise(id)
				end
				at ~ ~-1 ~
					for $step in $range(3)
						id = get_block()
						rotate_clockwise(id)
					end
				end
			end
		end
	end
end

function get_block()
	return -1
	switch block
		case *[facing=north]
			return $block_id
		end
		case *[facing=east]
			return $block_id
		end
		case *[facing=south]
			return $block_id
		end
		case *[facing=west]
			return $block_id
		end
	end
end

function rotate_clockwise(id)
	switch block id
		case piston[extended=true]

		end
		case sticky_piston[extended=true]

		end
		case *[facing=north]
			$rotated_block_state = $block_state.$replace('facing=north', 'facing=east')
			/setblock ~ ~ ~ $rotated_block_state
		end
		case *[facing=east]
			$rotated_block_state = $block_state.$replace('facing=east', 'facing=south')
			/setblock ~ ~ ~ $rotated_block_state
		end
		case *[facing=south]
			$rotated_block_state = $block_state.$replace('facing=south', 'facing=west')
			/setblock ~ ~ ~ $rotated_block_state
		end
		case *[facing=west]
			$rotated_block_state = $block_state.$replace('facing=west', 'facing=north')
			/setblock ~ ~ ~ $rotated_block_state
		end
	end
end
