dir "C:\Users\Seth\AppData\Roaming\.minecraft 1.14\saves\SethBling's World 1.14"
desc "See far-away things"

import common
import miniblock

define @Magnified: @Entity[type=falling_block, tag=Magnified]
	time: Time byte 1
end

define @NewPosition = @Entity[type=area_effect_cloud,tag=Position,limit=1]
	create {Tags:["NewPosition"], Duration:-1,Age:-2147483648,WaitTime:-2147483648}
end

reset
	unless magnification > 0
		magnification = 2
	end
end

clock main
	unless @Position do create @Position
	unless @NewPosition do create @NewPosition

	as @Player[limit=1]
		magnification = 0
		for $c in $range(66)
			if @s[nbt={SelectedItem:{tag:{spyglass:1b, magnification:$cb}}}]
				magnification = $c
			end
		end
		@s.magnification = magnification

		if magnification > 0
			if @s.rx == @s.prev_rx and @s.ry == @s.prev_ry and @s.<pos> == @s.<prev_pos> and @s.prev_mag == @s.magnification
			else
				/kill @Magnified

				rx = @s.rx
				ry = @s.ry

				first_match = True

				drx = 0
				dry = 0
				cast_magnifier_ray()

				for dx = 0 to 10000 by 2000
					for dy = 0 to 10000 by 2000
						drx = dx / magnification
						dry = dy / magnification
						cast_magnifier_ray()
						unless dx == 0
							drx = -dx / magnification
							cast_magnifier_ray()
						end
						unless dy == 0
							dry = -dy / magnification
							cast_magnifier_ray()
							unless dx == 0
								drx = dx / magnification
								cast_magnifier_ray()
							end
						end
					end
				end

				$border_offset = 0.6
				$border_pos = [
					($border_offset, 0),
					(-$border_offset, 0),
					(0, $border_offset),
					(0, -$border_offset),
					($border_offset, $border_offset),
					(-$border_offset, $border_offset),
					(-$border_offset, -$border_offset),
					($border_offset, -$border_offset),
				]
				for $side in $range($len($border_pos))
					$left, $up = $border_pos[$side]
					at @s eyes ^$left ^$up ^1.7
						as @MiniBlock[side==$side,limit=1]
							@s.rot_x = rx + 180000
							@s.rot_y = ry
							@s.update()
						else
							as create @MiniBlock
								@s.side = $side
								/replaceitem entity @s armor.head black_concrete
								@s.rot_x = rx + 180000
								@s.rot_y = ry
								@s.update()
							end
						end
					end
				end
			end


			@s.prev_rx = @s.rx
			@s.prev_ry = @s.ry
			@s.<prev_pos> = @s.<pos>
		else
			/kill @Magnified
			/kill @MiniBlock[side >= 0]
		end

		@s.prev_mag = magnification
	end

	for @bamboo in @Entity[type=item,nbt={Item:{id:"minecraft:bamboo", Count:1b}}]
		for $c in $range(2, 65)
			at @s unless @s[nbt={Item:{tag:{spyglass:1b}}}] as @Entity[type=item,nbt={Item:{id:"minecraft:glass_pane",Count:$cb}},distance=..0.5,limit=1] at @s
				/kill @s
				/kill @bamboo
				/particle poof ~ ~ ~ 0.5 0.5 0.5 0 5
				/summon item ~ ~ ~ {Item:{id:"minecraft:bamboo", Count:1b, tag:{spyglass:1b, magnification: $cb, display:{Name:"{\"text\":\"Spyglass ($cx)\", \"italic\":\"false\"}"},Enchantments:[{lvl:1s,id:"minecraft:infinity"}]}},PickupDelay:20s}
			end
		end
	end

	as @Magnified
		@s.time = 1
	end
end

function cast_magnifier_ray()
	at @s eyes ^ ^ ^ as @Position
		/tp @s ~ ~ ~ ~ ~
		@s.rx += drx
		@s.ry += dry

		at @s feet ^ ^ ^
			/tp @s ~ ~ ~
		end

		for step = 1 to 400
			at @s feet ^ ^ ^0.25
				/tp @s ~ ~ ~
			end

			at @s align xyz at ~0.5 ~0.5 ~0.5 unless block ~ ~ ~ air
				if first_match
					first_match = False
					<delta> = @s.<pos> - (@Player[limit=1].<pos> + <0, 1600, 0>)
					<offset> = <delta> - <delta> / magnification
				end

				/tp @s ~ ~ ~
				@s.<pos> -= <offset>

				#<delta> = @s.<pos> - @Player[limit=1].<pos>
				#<offset> = <delta> - <delta> / magnification
				#/tp @s ~ ~ ~
				#@s.<pos> -= <offset>

				create_magnified_block()
				step = 1000000
			end
		end
	end
end

function create_magnified_block()
	switch block
		default
			at @s unless @Magnified[distance=..0.01] if block ~ ~-0.5 ~ air
				/summon falling_block ~ ~-0.5 ~ {Time:1b, NoGravity:1b, BlockState:{$falling_block_nbt}, Tags:["Magnified","NewMagnified"]}
			end
		end
	end
end
