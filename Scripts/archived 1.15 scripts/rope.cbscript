dir: C:\Users\Seth\AppData\Roaming\.minecraft\saves\SethBling's World 1.13
desc: Create a rope.

$marker: "\"Marker\":1b, \"Invisible\":1b, \"NoGravity\":1b, \"Invulnerable\":1b"

define @Entity = @e
	x = Pos[0] double 1000
	y = Pos[1] double 1000
	z = Pos[2] double 1000
	vx = Motion[0] double 1000
	vy = Motion[1] double 1000
	vz = Motion[2] double 1000
end

@Player = @Entity[type=player]

define @Segment = @Entity[type=slime,name=Segment]
	create: {CustomName:"{\"text\":\"Segment\"}", Invulnerable:1b, Silent:1b}
	on_ground = OnGround byte 1

	function create_children()
		/team join rope
		if n == 0
			@s.last = True
			last = @s
		end
		if n > 0
			n--
			@s.next = create @Segment
			as @s.next(@Segment)
				@s.create_children()
			end
		end
	end

	function apply_tension()
		if @s.next
			my_x = @s.x
			my_y = @s.y
			my_z = @s.z

			as @s.next(@Segment)
				dx = @s.x - my_x
				dy = @s.y - my_y
				dz = @s.z - my_z

				dvx = dx * 80 / 100
				dvy = dy * 80 / 100
				dvz = dz * 80 / 100


				dvsq = dvx^2 + dvy^2 + dvz^2
				while dvsq > 100000000
					dvx /= 2
					dvy /= 2
					dvz /= 2
					dvsq = dvx^2 + dvy^2 + dvz^2
				end

				@s.vx -= dvx
				@s.vy -= dvy
				@s.vz -= dvz

				@s.on_ground = False

				if @s.last
					stuck = False
					at @s unless block ~ ~-1 ~ air then stuck = True
					at @s unless block ~1 ~ ~ air then stuck = True
					at @s unless block ~ ~ ~1 air then stuck = True
					at @s unless block ~-1 ~ ~ air then stuck = True
					at @s unless block ~ ~ ~-1 air then stuck = True

					@s.stuck = False
					/data merge entity @s {NoGravity:0b}
					if stuck and dvsq < 10000000
						@s.vx = 0
						@s.vy = 0
						@s.vz = 0
						@s.stuck = True
						/data merge entity @s {NoGravity:1b}
					end
				end
			end

			@s.vx += dvx
			@s.vy += dvy
			@s.vz += dvz

			as @s.next(@Segment)
				@s.apply_tension()
			end

			#@s.vx = @s.vx * 99 / 100
			#@s.vy = @s.vy * 99 / 100
			#@s.vz = @s.vz * 99 / 100
		end
	end

	function throw()
		@s.vx += throwx
		@s.vy += throwy
		@s.vz += throwz
		as @s.next(@Segment)
			@s.throw()
		end
	end
end

define @ClickDetector = @Entity[type=slime,name=ClickDetector]
	create: {CustomName:"\"ClickDetector\"", NoGravity:1b, NoAI: 1b, Invisible:1b, Silent:1b, Size:2}
	hp = AbsorptionAmount float 1
end

define @Marker = @Entity[type=armor_stand,name=Marker]
	create: {CustomName:"\"Marker\"", $marker}
end

define @Physics = @Entity[type=armor_stand,name=Physics]
	create: {CustomName:"\"Physics\"", Invisible:1b, Invulnerable:1b}
end

function create_rope()
	@s.rope = create @Segment
	as @s.rope(@Segment)
		n = 15
		@s.create_children()
	end
	@s.last = last

	at @s ~ ~200 ~
		@s.click = create @ClickDetector
	end
	as @s.click(@ClickDetector)
		@s.hp = 100
		/effect give @s minecraft:invisibility 1000000 0 true
	end
end

reset
	/kill @Segment
	/tp @ClickDetector ~ -200 ~
	/kill @ClickDetector
	/kill @Marker
	/kill @Physics

	/team add rope
	/team modify rope collisionRule pushOtherTeams

	for @CurPlayer in @a
		at @s
			create_rope()
		end
	end
end

clock main
	for @CurPlayer in @Player
		at @s ~ ~1.6 ~ rotated @s at ^ ^ ^15
			as @s.rope(@Segment)
				/tp @s ~ ~ ~
				@s.apply_tension()
			end
		end
		at @s ~ ~1.6 ~ rotated @s at ^ ^ ^3
			clicked = False
			as @s.click(@ClickDetector)
				/tp @s ~ ~ ~

				if @s.hp <= 97
					clicked = True
				end
				@s.hp = 100
			end

			if clicked
				stuck = False
				as @s.last do stuck = @s.stuck
				if stuck
					at @s do @s.physics = create @Physics
					as @s.last(@Segment)
						x1 = @s.x
						y1 = @s.y + 2000
						z1 = @s.z
					end

					as @s.physics(@Physics)
						/team join rope
						dx = (x1 - @s.x) / 10
						dy = (y1 - @s.y) / 10
						dz = (z1 - @s.z) / 10

						@s.vx = dx
						@s.vy = dy
						@s.vz = dz
						@s.player = @CurPlayer
					end
				end

				if not stuck
					as create @Marker
						/tp @s @CurPlayer
						x1 = @s.x
						y1 = @s.y
						z1 = @s.z
						at @s rotated @s
							/tp @s ^ ^ ^1
						end
						throwx = (@s.x - x1) * 3
						throwy = (@s.y - y1) * 3
						throwz = (@s.z - z1) * 3
						/kill @s
					end
					as @CurPlayer.rope(@Segment)
						@s.throw()
					end
				end

				if @s.rope == 0
					create_rope()
					as create @Marker
						/tp @s @CurPlayer
						x1 = @s.x
						y1 = @s.y
						z1 = @s.z
						at @s rotated @s
							/tp @s ^ ^ ^1
						end
						throwx = (@s.x - x1) * 3
						throwy = (@s.y - y1) * 3
						throwz = (@s.z - z1) * 3
						/kill @s
					end
					as @s.rope(@Segment)
						@s.throw()
					end
				end
			end
		end
	end
end

clock physics
	for @ph in @Physics
		at @s
			as @ph.player(@Player)
				/tp @s ~ ~ ~
			end
		end
		if @s.vx > -100 and @s.vx < 100 and @s.vz > -100 and @s.vz < 100 and @s.vy > -100 and @s.vy < 100
			/kill @s
		end
	end
end
