dir "C:\Users\Seth\AppData\Roaming\.minecraft 1.14\saves\SethBling's World 1.14"
desc "Breed blocks with each other"

import common
import tinyblock
import pushable
import music

$Blocks = [
	('command_block', 'redstone', 'enchant'),
	('note_block', 'bell', 'note'),
	('bookshelf', 'book', 'enchant'),
	('diamond_ore', 'diamond', 'block diamond_ore'),
	('piston', 'iron_ingot', 'block piston'),
]
$CommandBlock = 0
$NoteBlock = 1
$Bookshelf = 2
$DiamondOre = 3
$Piston = 4

$Mario = [
	(0, 12),
	(4, 12),
	(12, 12), 
	(20, 8),
	(24, 12),
	(32, 15),
	(48, 3),
]

$Cucaracha = [
	(0, 0),
	(4, 0),
	(8, 0),
	(12, 4),
	(24, 7),
	(32, 0),
	(36, 0),
	(40, 0),
	(44, 4),
	(56, 7),
]

$Yankee = [
	(0, 0),
	(4, 12),
	(8, 12),
	(12, 14),
	(16, 16),
	(20, 12),
	(24, 16),
	(28, 14),
	(32, 0),
	(36, 12),
	(40, 12),
	(44, 14),
	(48, 16),
	(52, 12),
	(60, 11),
]

$Zelda = [
	(0, 13),
	(3, 12),
	(6, 9),
	(9, 3),
	(12, 2),
	(15, 10),
	(18, 14),
	(21, 18),
]

$Mary = [
	(0, 10),
	(4, 8),
	(8, 6),
	(12, 8),
	(16, 10),
	(20, 10),
	(24, 10),
	(32, 8),
	(36, 8),
	(40, 8),
	(48, 10),
	(52, 13),
	(56, 13),
]

$Harry = [
	(0, 0),
	(4, 5),
	(12, 8),
	(14, 7),
	(16, 5),
	(24, 12),
	(28, 10),
	(40, 7),
	(52, 5),
	(60, 8),
	(62, 7),
	(64, 4),
	(72, 6),
	(76, 0),
]

$Simpsons = [
	(0, 6),
	(12, 10),
	(20, 12),
	(28, 15),
	(32, 13),
	(44, 10),
	(52, 6),
	(60, 3),
	(64, 0),
	(68, 0),
	(72, 0),
	(76, 1),
]

$Songs = [
	$Mario,
	$Cucaracha,
	$Yankee,
	$Zelda,
	$Mary,
	$Harry,
	$Simpsons,
]

define @BabyBlock: @Entity[type=slime,tag=BabyBlock]
	create {CustomName:"\"Baby\"", Tags:["BabyBlock"],Size:0, CustomNameVisible:0}
end

@Slime = @Entity[type=slime]

@Text = @Entity[type=armor_stand,tag=Text]
$Commands = [
	"/say Hello World!",
]

reset

end

clock main
	for @player in @Player
		for $i in $range($len($Blocks))
			$block_type, $breed_item, $particle = $Blocks[$i]
			if @s[nbt={SelectedItem:{id:"minecraft:$breed_item"}}]
				steps = 0
				at @s ~ ~1.6 ~
					while block ^ ^ ^ air and steps < 100 at ^ ^ ^0.1
						steps++
						if block ~ ~ ~ $block_type
							@Pushable.old = True
							pushables:create_block()
							as @Pushable unless @s.old
								@s.breed_type = $i
								@s.solidify = 0
							end
							steps = 1000
						end
					end
				end
			end
		end
	end
	
	for $i in $range($len($Blocks))
		$breed_item = $Blocks[$i][1]
		
		for @pushable in @Pushable[breed_type == $i]
			at @s
				if @s.hop_cooldown > 0
					@s.hop_cooldown--
				else if @s.on_ground
					@Entity.target = False
					if @s.willing and @s.breed_type == $Piston
						@Slime[distance=1..8,willing,limit=1,sort=nearest].target = True
					end
					if @s.willing unless @Entity[target]
						@Pushable[distance=1..8,willing,breed_type==$i,limit=1,sort=nearest].target = True
					end
					unless @Entity[target]
						@Player[distance=..8,nbt={SelectedItem:{id:"minecraft:$breed_item"}},limit=1,sort=nearest].target = True
					end
					facing @Entity[target,limit=1]
						@s.solidify = 0
						/tp @s ~ ~ ~ ~ ~
						unless @Position do create @Position
						/tp @Position ~ ~ ~ ~ ~
						<pos1> = @Position.<pos>
						move @Position ^ ^ ^1
						<dx,dy,dz> = @Position.<pos> - <pos1>
						@s.<vel> = <dx, 1700, dz> / 3
						@s.hop_cooldown = rand(30, 50)
						/playsound minecraft:entity.slime.jump neutral @a
					end
				end
				
				
				if @s.willing
					at ~ ~1 ~
						if @s.heart_cooldown > 0
							@s.heart_cooldown--
						else
							/particle minecraft:heart ~ ~ ~ 0.2 0.2 0.2 0.2 1 force
							@s.heart_cooldown = 10
						end
						@s.willing--
					end
					@Entity.target = False
					if @s.breed_type == $Piston
						@Slime[willing,distance=0.1..1.2,limit=1].target = True
					end
					if @Entity[target]
						sticky = True
					else
						sticky = False
						@Pushable[willing, distance=0.1..1.2, breed_type == $i,limit=1].target = True
					end
					
					as @Entity[target]
						@s.willing = 0
						@pushable.willing = 0
						breed_type = @s.breed_type
						as create @BabyBlock ~ 300 ~
							$block_type, $breed_item, $particle = $Blocks[$i]
							
							@s.breed_type = $i
							
							/tp @s ~ ~ ~
							
							id = &@s
							@s.tinyblock = create @TinyBlock
							@s.sticky = sticky
							as @s.tinyblock
								@s.babyblock = id
								@s.sticky = sticky
								if sticky
									/replaceitem entity @s weapon.mainhand sticky_piston
								else
									/replaceitem entity @s weapon.mainhand $block_type
								end
							end
							
							/particle minecraft:$particle ~ ~ ~ 0.5 0.5 0.5 0.5 10 normal
							/summon experience_orb
							/summon experience_orb
							/summon experience_orb
						
							if @s.breed_type == $NoteBlock
								$num_songs = $len($Songs)
								@s.song = rand(0, $num_songs)
								#@s.song = 5
								@s.age = 0
							else if @s.breed_type == $CommandBlock
								$num_commands = $len($Commands)
								switch rand(0, $num_commands)
									case $i in $range($num_commands)
										$command = $Commands[$i]
										at @s
											/summon armor_stand ~ ~1 ~ {CustomName:"\"$command\"",CustomNameVisible:1,Invisible:1,Marker:1,Invulnerable:1b, Tags:["Text"]}
											/$command
										end
									end
								end
							end
						end
					end
				else
					if success
					/kill @e[type=item,nbt={Item:{id:"minecraft:$breed_item"}},distance=..1]
						@s.willing = 300
					end
				end
				
				@s.solidify++
				if @s.solidify >= 100
					unless @Position do create @Position
					/tp @Position ~ ~ ~
					<pos1> = @Position.<pos>
					align xyz at ~0.5 ~ ~0.5
						/tp @Position ~ ~ ~
						<dx, dy, dz> = @Position.<pos> - <pos1>
					end
					@s.<vel> += <dx, 0, dz> / 15
					if dx > -30 and dx < 30 and dy > -30 and dy < 30 and dz > -30 and dz < 30
						switch @s.breed_type
							case $i in $range($len($Blocks))
								$block_type = $Blocks[$i][0]
								if @s.sticky
									/setblock ~ ~ ~ sticky_piston
								else
									/setblock ~ ~ ~ $block_type
								end
							end
						end
						
						as @SolidBlock[distance=..2]
							@s.kill_me = True
						end
						/kill @Physics[distance=..1]
						/kill @Disguise[distance=..1]
						/kill @FakeBlock[distance=..1]
						/kill @s
					end
				end
			end
		end
	end
	
	as @Slime unless @s[tag=BabyBlock] at @s
		if success
		/kill @e[type=item,nbt={Item:{id:"minecraft:iron_ingot"}},distance=..2]
			@s.willing = 300
		end
		if @s.willing at ~ ~1 ~
			if @s.heart_cooldown > 0
				@s.heart_cooldown--
			else
				/particle minecraft:heart ~ ~ ~ 0.2 0.2 0.2 0.2 1 force
				@s.heart_cooldown = 10
			end
			@s.willing--
		end
	end
	
	/tp @SolidBlock[kill_me] ~ -200 ~
	
	as @BabyBlock at @s
		switch @s.song
			case $s in $range($len($Songs))
				$song = $Songs[$s]
				
				for $note in $song
					$t = $note[0]
					$p = $note[1]
					if @s.age == $t
						$playsound('block.note_block.flute', $p)
					end
				end
			end
		end
	
		@s.age++
		/effect give @s invisibility 10 1 true
		as @s.tinyblock
			@s.update_tiny()
			@s.fire = 10
		else
			/kill @s
		end
		if @s.age >= 200 and block ~ ~ ~ air
			switch @s.breed_type
				case $i in $range($len($Blocks))
					$block_type = $Blocks[$i][0]
					if @s.sticky
						/setblock ~ ~ ~ sticky_piston
						sticky = True
					else
						/setblock ~ ~ ~ $block_type
						sticky = False
					end
					@Pushable.old = True
					pushables:create_block()
					as @Pushable unless @s.old
						if sticky
							@s.sticky = True
						end
						@s.breed_type = $i
						@s.solidify = 0
					end
				end
			end
			/tp @s ~ -200 ~
			/kill @s
		end
	end
	as @TinyBlock[babyblock]
		as @s.babyblock
		else
			/kill @s
		end
	end
	as @Text
		move @s ~ ~0.05 ~
		@s.age++
		if @s.age >= 60
			/kill @s
		end
	end
end

function kill_all()
	/kill @BabyBlock
	/kill @TinyBlock[babyblock]
end