dir "C:\Users\Seth\AppData\Roaming\.minecraft 1.14\saves\SethBling's World 1.14"
desc "Experience Zero-G"
scale 10000

import common

define @Position = @Entity[type=area_effect_cloud,tag=Position,limit=1]
	create: {Tags:["Position"], Duration:-1,Age:-2147483648,WaitTime:-2147483648}
end

@SplashPotion = @Entity[type=potion]

reset

end

clock main
	for @player in @Player
		holding_potion = False
		/execute if entity @s[nbt={SelectedItem:{id:"minecraft:splash_potion"}}] run scoreboard players set Global holding_potion 1
		
		if holding_potion
			unless @Position do create @Position
			as @Position
				at @player
					/tp @s ~ ~ ~ ~ ~
				end
				
				<pos> = @s.<pos>
				move @s ^ ^ ^1
				<delta> = (@s.<pos> - <pos>) / 2
				
				<pos> += <0, 23000, 0>
				
				for i = 1 to 100
					<delta> *= 990
					<delta> /= 1000
					<delta> -= <0, 400, 0>
					<pos> += <delta>
					@s.<pos> = <pos>
					at @s
						draw = i % 5
						if draw == 0
							/particle fishing ~ ~ ~ 0 0 0 0 1 force @player
						end
						unless block ~ ~ ~ air
							<pos> -= <delta>
							@s.<pos> = <pos>
							at @s
								/particle explosion ~ ~ ~ 0 0 0 0 1 force @player
							end
							i = 100
							<explosion_pos> = <pos>
						end
					end
				end
			end
		end
	end
end

clock debug
	as @SplashPotion
		<splash_pos> = @s.<pos>
		<x, y, z> = @s.<vel>
		tell @a: (x) (y) (z)
		display_error = True
	end
	unless @SplashPotion if display_error
		<x, y, z> = <splash_pos> - <explosion_pos>
		tell @a: (x) (y) (z)
		display_error = False
	end
end
