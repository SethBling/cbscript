dir "C:\Users\Seth\AppData\Roaming\.minecraft 1.14\saves\SethBling's World 1.14"
desc "Breed blocks with each other"

import common
import tinyblock
import miniblock

$Stalk		= 1
$Leaf		= 2
$Mouth		= 3
$SmallHead	= 4
$BigHead	= 5

define @Plant : @Entity[type=area_effect_cloud, tag=PiranhaPlant]
	create {Tags:["PiranhaPlant"], Duration:-1,Age:-2147483648,WaitTime:-2147483648}
	
	array stalk[0 to 7]
	array miniblocks[0 to 11]
	array mouth[0 to 5]
	
	function kill()
		for $i in $range(8)
			as @s.stalk[$i]
				/kill @s
			end
		end
		for $i in $range(12)
			as @s.miniblocks[$i]
				/kill @s
			end
		end
		for $i in $range(6)
			as @s.mouth[$i]
				/kill @s
			end
		end
		/kill @s
	end
end

define @Fireball : @Entity[type=slime,tag=Fireball]
	create {Size:0, Tags:["Fireball"], Fire: 10000s, Invulnerable:1, Silent:1}
	size: Size byte 1
end

reset
	/scoreboard objectives add plant_wheat minecraft.used:minecraft.wheat_seeds
	
	tell @a '{G[\[Give Piranha Plant Seed\]](call give_seed)'
end

function give_seed()
	/give @s wheat_seeds{piranha:1b, display:{Name:"{\"text\":\"Piranha Plant Seeds\", \"italic\":\"false\"}"}}
end

clock main
	as @Player at @s eyes ^ ^ ^
		if @s.plant_wheat and @s.holding_seed
			steps = 0
			while steps < 60 at feet ^ ^ ^0.1
				if block ~ ~ ~ wheat and block ~ ~-1 ~ farmland align xyz at ~0.5 ~ ~0.5
					as create @Plant
						@s.current_stage = -1
					end
					steps = 1000000
				end
				steps++
			end
		end
		if @s[nbt={SelectedItem:{tag:{piranha:1b}}}]
			@s.holding_seed = True
		else
			@s.holding_seed = False
		end
	end
	
	@Player.plant_wheat = 0
	
	as @Plant at @s
		stage = -1
		for $i in $range(8)
			/execute if block ~ ~ ~ wheat[age=$i] run scoreboard players set Global stage $i
			# TODO: kill any wheat seeds that were just spawned
			if current_stage >= 7
				# TODO: Drop seeds
			end
		end
		if stage == -1
			@s.kill()
		end
		unless stage == @s.current_stage
			for $i in $range(8)
				if stage >= $i
					$offset = $i * 0.375 - 0.225
					$create_offset = $offset-0.5
					as @s.stalk[$i]
					else
						@s.stalk[$i] = create @TinyBlock ~0.0625 ~$create_offset ~-0.375
						id = &@s
						as @s.stalk[$i]
							/replaceitem entity @s weapon.mainhand minecraft:cactus
							@s.parent = id
							@s.type = $Stalk
							@s.segment = $i
						end
					end
					as @s.stalk[$i]
						at ~ ~$offset ~
							@s.update_tiny()
						end
						@s.stage = stage
					end
				else
					as @s.stalk[$i]
						/kill @s
					end
				end
			end
			if stage >= 2
				for $i in $range(2)
					id = &@s
					as @s.miniblocks[$i]
					else
						@s.miniblocks[$i] = create @MiniBlock
					end
					as @s.miniblocks[$i]
						@s.parent = id
						@s.type = $Leaf
						/replaceitem entity @s armor.head lime_carpet
						switch stage
							case $stage in $range(2, 8)
								$offx = $i - 0.5
								$offy = 0.5 + $stage * 0.2 - $i * 0.2
								at ~$offx ~$offy ~
									@s.update()
								end
							end
						end
					end
				end
			else
				for $i in $range(2)
					as @s.miniblocks[$i]
						/kill @s
					end
				end
			end
			if stage >= 4 and stage < 7
				id = &@s
				as @s.miniblocks[3]
				else
					@s.miniblocks[3] = create @MiniBlock
				end
				as @s.miniblocks[3]
					@s.parent = id
					@s.type = $SmallHead
					/replaceitem entity @s armor.head red_mushroom_block
					switch stage
						case $stage in $range(4, 7)
							$offy = 0.375 * $stage + 0.55
							at ~ ~$offy ~
								@s.update()
							end
						end
					end
				end
			else
				as @s.miniblocks[3]
					/kill @s
				end
			end
			if stage == 7
				for $i in $range(4, 12)
					id = &@s
					as @s.miniblocks[$i]
					else
						@s.miniblocks[$i] = create @MiniBlock
					end
					as @s.miniblocks[$i]
						@s.parent = id
						@s.type = $BigHead
						/replaceitem entity @s armor.head red_mushroom_block
						$idx = $i - 4
						$offx = ($idx % 2) * 0.6 - 0.3
						$offz = ($idx / 2) % 2 * 0.6 - 0.3
						$offy = ($idx / 4) * 0.6 + 7 * 0.375 + 0.6
						at ~$offx ~$offy ~$offz
							@s.update()
						end
					end
				end
				
				for $i in $range(6)
					id = &@s
					as @s.mouth[$i]
					else
						@s.mouth[$i] = create @TinyBlock
					end
					as @s.mouth[$i]
						@s.type = $Mouth
						@s.parent = id
						/replaceitem entity @s weapon.mainhand white_wool
						$direction = (($i / 3) * 2 - 1)
						@s.direction = $direction
						$offx = ($i / 3) * 0.8 - 0.4
						$offy = 7 * 0.375 + 1.1
						$offz = ($i % 3) * 0.375 - 0.375
						at ~$offx ~$offy ~$offz
							@s.update_tiny()
						end
					end
				end
			else
				for $i in $range(4, 12)
					as @s.miniblocks[$i]
						/kill @s
					end
				end
				for $i in $range(6)
					as @s.mouth[$i]
						/kill @s
					end
				end
			end
			if stage == 7
				@s.anim_timer = 0
			end
			
			@s.current_stage = stage
		end
		if stage == 7
			update_anim_timer()
			
			if @s.shoot_fire
				if @s.anim_timer == 80
					spawn_fireball()
				end
				
				if @s.anim_timer == 100
					spawn_fireball()
				end
			end
			
			if @s.eat_time > 0
				@s.eat_time--
			end
			switch @s.eat_time
				case $i in [2, 7, 12]
					/playsound minecraft:entity.generic.eat block @a
				end
				case 1
					@s.shoot_fire = True
				end
			end
		end
	end
	
	
				
	as @Entity[type=armor_stand]
		if @s.type == $Mouth
			update_anim_timer()
			switch @s.anim_timer
				case 0 to 19
					move @s ~ ~-0.12 ~
					@s.dy -= 120
				end
				case 60 to 79
					move @s ~ ~0.12 ~
					@s.dy += 120
				end
			end
			
			@s.fire = 10
			switch @s.anim_timer % 15
				case 0 to 6
					dx = @s.direction * -34
					@s.x += dx
				end
				case 8 to 14
					dx = @s.direction * 34
					@s.x += dx
				end
			end
		end
		
		if @s.type == $Stalk and @s.segment >= 4 and @s.stage == 7
			update_anim_timer()
			@s.fire = 10
			small_updown()
		end
		
		if @s.type == $Leaf
			update_anim_timer()
			@s.fire = 10
			small_updown()
		end
	
		if @s.type == $BigHead
			update_anim_timer()
			@s.fire = 10
			updown()
			at @s
				/effect give @Entity[distance=..1] instant_damage 1 0 true
				if success
				/kill @e[type=item,nbt={Item:{id:"minecraft:flint_and_steel"}},distance=..1]
					as @s.parent
						@s.eat_time = 13
					end
				end
			end
		end
	end
	
	as @Fireball at @s
		/effect give @s invisibility 10 0 true
		kill = False
		if @s.vx == 0 then kill = True
		if @s.vz == 0 then kill = True
		if @s.on_ground
			@s.vy = 500
			@s.on_ground = False
		end
		@s.vx = @s.dx
		@s.vz = @s.dz
		
		at ~ ~-1 ~ if @Player[distance=..1.2]
			/effect give @Player[distance=..1.2] instant_damage 1 0 true
			kill = True
		end
		if block ~ ~ ~ water
			kill = True
		end

		if kill
			/particle poof ~ ~ ~ 0.5 0.5 0.5 0 5
			/tp @s ~ -100 ~
			/kill @s
		end
	end
end

function update_anim_timer()
	@s.anim_timer++
	if @s.anim_timer == 119
		@s.anim_timer = -1
	end
end

function updown()
	switch @s.anim_timer
		case 0 to 19
			move @s ~ ~-0.12 ~
		end
		case 60 to 79
			move @s ~ ~0.12 ~
		end
	end
end

function small_updown()
	switch @s.anim_timer
		case 0 to 19
			move @s ~ ~-0.062 ~
		end
		case 60 to 79
			move @s ~ ~0.062 ~
		end
	end
end

function spawn_fireball()
	at ~ ~4 ~ facing @Player[distance=..32,sort=nearest,limit=1]
		/playsound minecraft:entity.blaze.shoot hostile @a
		unless @Position do create @Position
		as @Position
			/tp @s ~ ~ ~ ~ 0
			<pos1> = @s.<pos>
			move @s ^ ^ ^1
			<dx, dy, dz> = @s.<pos> - <pos1>
		end
		as create @Fireball
			@s.<vel> = <dx/2, 500, dz/2>
			@s.dx = dx/2
			@s.dz = dz/2
		end
	end
end